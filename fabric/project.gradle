// Hat tip to JellySquid
configurations {
    modIncludeImplementation
    modImplementation.extendsFrom modIncludeImplementation
    include.extendsFrom modIncludeImplementation
}

dependencies {
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation(fabricApi.module("fabric-api-base", project.fabric_version))
	modImplementation(fabricApi.module("fabric-resource-loader-v0", project.fabric_version))
	modImplementation(fabricApi.module("fabric-object-builder-api-v1", project.fabric_version))
	modImplementation(fabricApi.module("fabric-networking-api-v1", project.fabric_version))

	// Note that BitKit is included via FREX
	modIncludeImplementation("io.vram:frex-fabric-mc119:6.1.297:fat")
	modIncludeImplementation("io.vram:littlegui-fabric-mc119:1.0.8")

	modIncludeImplementation "blue.endless:jankson:1.2.1"
	modIncludeImplementation("io.vram:special-circumstances:1.10.7")

	modCompileOnly "com.terraformersmc:modmenu:4.0.6"
	modRuntimeOnly "com.terraformersmc:modmenu:4.0.6"
}

sourceSets {
	main {
		java {
			srcDirs = ['../common/src/main/java', 'src/main/java']
			include '**/*'
			exclude '*.DS_Store'
		}
		resources {
			srcDirs = ['../common/src/main/resources', 'src/main/resources', '../textures/core/src/main/resources', '../textures/tech/src/main/resources', '../textures/test/src/main/resources', '../textures/unstable/src/main/resources']
			include '**/*'
			exclude '*.DS_Store'
		}
	}
	test {
		java {
			exclude '**/*'
		}
	}
}
